# Blimp SCons rules for bundling on windows

Import('dcraw', 'install_base', 'install_bin', 'install_lib', 'xerces_jar')

import os

jsmoothcmd = os.path.join(os.environ['PROGRAMFILES'], 'JSmooth 0.9.9-7', 'jsmoothcmd.exe')

Install(install_base, 'COPYING')
InstallAs(install_bin + '/blimp-dcraw.exe', dcraw)

# For now, only bundle 32-bit version of SWT
swt_jar = 'swt-3.4-win32/swt.jar'
Install(install_lib, [xerces_jar, swt_jar])

Install(install_base, [
    'tools/bundle-windows/blimp.jsmooth',
    'icons/blimp.ico',
])

if os.path.isfile(jsmoothcmd):
    print '--- JSmooth detected:', jsmoothcmd
    env = Environment()
    env['JSMOOTHCMD'] = jsmoothcmd
    env['BUILDERS']['JSmooth'] = Builder(action = '"$JSMOOTHCMD" $SOURCE')
    blimp_exe = env.JSmooth(install_bin + '/blimp.exe', install_base + '/blimp.jsmooth')
else:
    print '*** WARNING: JSmooth not detected'

def query_wix_dir():
    import _winreg
    for hkey in [_winreg.HKEY_LOCAL_MACHINE, _winreg.HKEY_CURRENT_USER]:
        try:
            key = _winreg.OpenKey(hkey, r'SOFTWARE\Microsoft\Windows Installer XML\3.0')
            value, type = _winreg.QueryValueEx(key, 'InstallRoot')
            return value
        except WindowsError:
            pass
    print '*** WARNING: failed to detect Windows Installer XML 3.0 folder'
    return ''

wix_dir = query_wix_dir()
if wix_dir:
    print '--- WiX detected:', wix_dir
    env = Environment()
    env.Tool('wixtool', ['tools/scons'])
    env['ENV']['PATH'] = wix_dir
    env['WIXLIGHTFLAGS'] = '-nologo -ext WixUIExtension'
    blimp_msi = env.WiX('Blimp.msi', [
        'tools/bundle-windows/Blimp.wxs',
        'tools/bundle-windows/WixUI_InstallDir_NoEula.wxs',
        ])
    env.Depends(blimp_msi, [install_base, blimp_exe])
    env.Alias('msi', blimp_msi)

    env = Environment()
    systemdir = os.path.join(os.environ['WINDIR'],'system32')
    msiexec = os.path.join(systemdir, 'msiexec.exe')
    if os.path.isfile(msiexec):
        env['ENV']['PATH'] = systemdir
    else:
        print '*** WARNING: msiexec not found, expected it at:', msiexec
    
    env['BUILDERS']['InstallMsi'] = Builder(action = 'msiexec /quiet /i $SOURCE')
    # not generic at all... could parse Blimp.wxs to find the GUID? 
    env['BUILDERS']['UninstallBlimp'] = Builder(action = 'msiexec /quiet /x {8F934390-ADB9-4071-B0CC-D9B13E0236F0}')
    
    dummy_target = env.InstallMsi('dummy-msi-target', 'Blimp.msi')
    env.Alias('install-msi', dummy_target)
    dummy_target = env.UninstallBlimp('dummy-uninstall-target', [])
    env.Alias('uninstall-msi', dummy_target)
